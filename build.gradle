plugins {
    id 'java'
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

group = 'plazmer'
version = '2.1.2'

repositories {
    mavenCentral()

    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }

    // Spigot API * start
    maven {
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'

        // As of Gradle 5.1, you can limit this to only those
        // dependencies you expect from it
        content {
            includeGroup 'org.bukkit'
            includeGroup 'org.spigotmc'
        }
    }
    maven { url "https://repo.dmulloy2.net/repository/public/" }
    // Spigot API * end

    // Holographic Displays
    maven { url = 'https://repo.codemc.io/repository/maven-public/' }
    //WorldGuard API
    maven { url = 'https://maven.enginehub.org/repo/' }
    // PAPI
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }

    maven { url = 'https://jitpack.io' }
    maven { url = 'https://libraries.minecraft.net/' }
    maven { url = 'https://nexus.phoenixdevt.fr/repository/maven-public/' }

    // griefdefender
    maven { url = 'https://repo.glaremasters.me/repository/bloodshot' }

    // PaF
    maven { url = 'https://simonsator.de/repo/' }

    // towny
    maven { url = 'https://repo.glaremasters.me/repository/towny/' }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.20.1-R0.1-SNAPSHOT'

    // worldedit
    compileOnly 'com.sk89q.worldedit:worldedit-bukkit:7.2.15-SNAPSHOT'
    compileOnly 'com.sk89q.worldedit:worldedit-core:7.2.15-SNAPSHOT'


    // party
    compileOnly 'de.simonsator:spigot-party-api-for-party-and-friends:1.0.7-RELEASE'
    compileOnly 'de.simonsator:Party-and-Friends-MySQL-Edition-Spigot-API:1.5.4-RELEASE'

    // regions
    compileOnly 'com.palmergames.bukkit.towny:towny:0.100.0.0'
    compileOnly group: 'com.griefdefender', name: 'api', version: '2.1.0-SNAPSHOT'
    compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.0.2-SNAPSHOT'
    compileOnly 'com.github.cryptomorin:kingdoms:1.16.8.1.1'
    compileOnly 'com.github.getplusm:ProtectionBlocks:1.0.6R1'

    compileOnly 'com.github.TechFortress:GriefPrevention:16.18'
    compileOnly 'com.github.angeschossen:LandsAPI:6.33.10'

    // holo
    compileOnly 'me.filoghost.holographicdisplays:holographicdisplays-api:3.0.0'
    compileOnly 'com.github.decentsoftware-eu:decentholograms:2.8.3'

    compileOnly 'com.github.LoneDev6:API-ItemsAdder:3.5.0b'
    compileOnly 'net.Indyuce:MMOItems-API:6.9.2-SNAPSHOT'
    compileOnly 'io.lumine:MythicLib-dist:1.5.2-SNAPSHOT'

    // PAPI
    compileOnly 'me.clip:placeholderapi:2.11.3'
    compileOnly 'com.mojang:authlib:1.5.21'

    // LP
    compileOnly 'net.luckperms:api:5.4'
    // Vault
    compileOnly 'com.github.MilkBowl:VaultAPI:1.7'

    compileOnly 'com.github.getplusm:Engine:2.4.4'
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
shadowJar {
    archivesBaseName = "TheDungeons"
    archiveClassifier.set ""
    destinationDirectory.set(rootProject.buildDir)
}
assemble.dependsOn(shadowJar)
// you can change broadcast settings in editor
// all dungeons messages send only for world players